dnl Process this file with autoconf to produce a configure script.
AC_INIT(include/pi-cmp.h)
AC_CONFIG_AUX_DIR(scripts)

dnl Check host type
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h malloc.h sys/ioctl.h sys/time.h sys/ioctl_compat.h memory.h string.h strings.h unistd.h stdlib.h netinet/in.h dirent.h sys/ndir.h sys/dir.h ndir.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(atexit strchr memcpy strtoul cfmakeraw cfsetspeed cfsetispeed cfsetospeed sigaction)

AC_CACHE_CHECK([for cispeed and cospeed members of struct termios],
  ac_cv_termios_cspeed,
[AC_TRY_COMPILE([#include <termios.h>], [int main(void) {
 struct termios t;t.c_ispeed=t.c_ospeed=0;}],
  ac_cv_termios_cspeed=yes,ac_cv_termios_cspeed=no)])
if test $ac_cv_termios_cspeed = yes; then
  AC_DEFINE(TERMIOS_CSPEED)
fi

dnl Derive warning flag
AC_CACHE_CHECK([for maximum warnings compiler flag],
  ac_cv_cwflag,
[case "${CC-cc}" in
  *gcc*) ac_cv_cwflag=-Wall;;
  *)	case "$host" in
			*irix*)	ac_cv_cwflag=-fullwarn ;;
			*)		ac_cv_cwflag=;;
		esac ;;
esac])
CWFLAG=$ac_cv_cwflag
AC_SUBST(CWFLAG)

dnl NeXT support
case "$host" in
    *next*) CPLIB="ln -s"
            ARFLAGS="cur" ;;
    *)      CPLIB="cp"
            ARFLAGS="cur" ;;
esac
AC_SUBST(CPLIB)
AC_SUBST(ARFLAGS)

AC_CONFIG_HEADER(include/pi-config.h)
AC_OUTPUT(lib/Makefile Makefile)
